locals {
  ssh_public_key = file(var.ssh_public_key_path)
  db_vms_map     = { for vm in var.each_vm : vm.vm_name => vm }
}

resource "yandex_compute_instance" "db" {
  for_each = local.db_vms_map

  name = each.key

  resources {
    cores  = each.value.cpu
    memory = each.value.ram
  }

  boot_disk {
    initialize_params {
      image_id = data.yandex_compute_image.ubuntu.image_id
      size     = each.value.disk_volume
      type     = each.value.disk_type
    }
  }

  network_interface {
    subnet_id          = yandex_vpc_subnet.develop.id
    security_group_ids = [yandex_vpc_security_group.example.id]
  }

  metadata = {
    ssh-keys = "ubuntu:${local.ssh_public_key}"
  }

  depends_on = [yandex_compute_instance.web]
}

output "db_instances" {
  value = {
    for name, instance in yandex_compute_instance.db :
    name => {
      "id"       = instance.id
      "name"     = instance.name
      "fqdn"     = instance.fqdn
      "ip"       = instance.network_interface.0.nat_ip_address
      "cpu"      = instance.resources[0].cores
      "ram"      = instance.resources[0].memory
      "disk"     = instance.boot_disk[0].initialize_params[0].size
      "disk_type" = instance.boot_disk[0].initialize_params[0].type
    }
  }
  description = "Detailed information about DB instances"
}